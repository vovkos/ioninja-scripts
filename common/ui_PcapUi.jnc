//..............................................................................
//
// Pcap device user interface
//

import "ui_ComboBox.jnc"
import "ui_ToolBar.jnc"
import "ui_PropertyGrid.jnc"
import "io_pcap.jncx"

namespace ui {

//! \addtogroup common-ui
//! @{

//..............................................................................

EnumPropertyOption const* createPcapDeviceOptionArray() {
	size_t count;
	io.PcapDeviceDesc const* device = io.createPcapDeviceDescList(&count);
	EnumPropertyOption* optionArray = new EnumPropertyOption[count];
	EnumPropertyOption* option = optionArray;

	for (; device; device = device.m_next, option++) {
		option.m_text = $"%1 - %2" (
			device.m_description ? device.m_description : device.m_name,
			device.m_address.m_address.getString()
		);

		option.m_value = device;
	}

	return optionArray;
}

//..............................................................................

EnumProperty* createPcapDeviceProperty(
	PropertyGrid* propertyGrid,
	GroupProperty* group = null,
	Property* beforeProp = null,
	string_t name,
	string_t toolTip
) {
	EnumPropertyOption const* optionArray = createPcapDeviceOptionArray();
	size_t optionCount = dynamic countof(optionArray);

	return propertyGrid.createEnumProperty(
		group,
		beforeProp,
		name,
		toolTip,
		optionArray,
		optionCount
	);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

ComboBox* createPcapDeviceComboBox(
	ToolBar* toolBar,
	uint_t width = -1
) {
	EnumPropertyOption const* optionArray = createPcapDeviceOptionArray();
	size_t optionCount = dynamic countof(optionArray);

	ComboBox* comboBox = toolBar.addComboBox(width);

	for (size_t i = 0; i < optionCount; i++)
		comboBox.addItem(
			optionArray[i].m_text,
			optionArray[i].m_value
		);

	return comboBox;
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

size_t enumeratePcapDevices(
	ComboBox* deviceCombo,
	EnumProperty* deviceProp
) {
	EnumPropertyOption const* optionArray = createPcapDeviceOptionArray();
	size_t optionCount = dynamic countof(optionArray);

	size_t index = -1;

	io.PcapDeviceDesc const* device = deviceCombo.m_currentData;
	if (device)
		for (size_t i = 0; i < optionCount; i++) {
			io.PcapDeviceDesc const* device2 = optionArray[i].m_value;
			if (device.m_name == device2.m_name) {
				index = i;
				break;
			}
		}

	deviceCombo.setItems(optionArray, optionCount);
	deviceProp.setOptions(optionArray, optionCount);

	if (index != -1) {
		deviceCombo.m_currentIndex = index;
		deviceProp.m_currentIndex = index;
	}

	return optionCount;
}

//..............................................................................


//! @}

} // namespace ui
