//..............................................................................
//
// Socket & network adapter user interface
//

import "ui_PropertyGrid.jnc"
import "ui_ComboBox.jnc"
import "ui_Layout.jnc"
import "doc_Storage.jnc"
import "std_Array.jnc"
import "io_base.jncx"
import "io_Serial.jnc"
import "io_SerialPortEnumerator.jnc"

namespace ui {

//! \addtogroup common-ui
//! @{

//..............................................................................

class SerialSettingUi {
	bitflag enum SettingId {
		BaudRate,
		DataBits,
		Parity,
		StopBits,
		FlowControl,

		All =
			BaudRate |
			DataBits |
			Parity |
			StopBits |
			FlowControl,
	}

	enum Defaults {
		BaudRate    = 38400,
		DataBits    = 8,
		Parity      = io.SerialParity.None,
		StopBits    = io.SerialStopBits._1,
		FlowControl = io.SerialFlowControl.None,
	}

	ui.ComboBox* m_baudRateCombo;
	ui.ComboBox* m_dataBitsCombo;
	ui.ComboBox* m_parityCombo;
	ui.ComboBox* m_stopBitsCombo;
	ui.ComboBox* m_flowControlCombo;

	ui.ComboProperty* m_baudRateProp;
	ui.EnumProperty* m_dataBitsProp;
	ui.EnumProperty* m_parityProp;
	ui.EnumProperty* m_stopBitsProp;
	ui.EnumProperty* m_flowControlProp;

	property m_baudRate {
		uint_t get() {
			return
				m_baudRateCombo ? atoi(m_baudRateCombo.m_editText) :
				m_baudRateProp ? atoi(m_baudRateProp.m_value) :
				Defaults.BaudRate;
		}

		void set(uint_t value) {
			if (m_baudRateCombo)
				m_baudRateCombo.m_editText = $"$value bps";
			else if (m_baudRateProp)
				m_baudRateProp.m_value = $"$value bps";
		}
	}

	property m_dataBits {
		uint_t get() {
			return
				m_dataBitsCombo ? m_dataBitsCombo.m_currentData :
				m_dataBitsProp ? m_dataBitsProp.m_value :
				Defaults.DataBits;
		}

		void set(uint_t value) {
			if (m_dataBitsCombo)
				m_dataBitsCombo.m_currentData = value;
			else if (m_dataBitsProp)
				m_dataBitsProp.m_value = value;
		}
	}

	property m_parity {
		io.SerialParity get() {
			return
				m_parityCombo ? m_parityCombo.m_currentData :
				m_parityProp ? m_parityProp.m_value :
				Defaults.Parity;
		}

		void set(io.SerialParity value) {
			if (m_parityCombo)
				m_parityCombo.m_currentData = value;
			else if (m_parityProp)
				m_parityProp.m_value = value;
		}
	}

	property m_stopBits {
		io.SerialStopBits get() {
			return
				m_stopBitsCombo ? m_stopBitsCombo.m_currentData :
				m_stopBitsProp ? m_stopBitsProp.m_value :
				Defaults.StopBits;
		}

		void set(io.SerialStopBits value) {
			if (m_stopBitsCombo)
				m_stopBitsCombo.m_currentData = value;
			else if (m_stopBitsProp)
				m_stopBitsProp.m_currentIndex = value;
		}
	}

	property m_flowControl {
		io.SerialFlowControl get() {
			return
				m_flowControlCombo ? m_flowControlCombo.m_currentData :
				m_flowControlProp ? m_flowControlProp.m_value :
				Defaults.FlowControl;
		}

		void set(io.SerialFlowControl value) {
			if (m_flowControlCombo)
				m_flowControlCombo.m_currentData = value;
			else if (m_flowControlProp)
				m_flowControlProp.m_value = value;
		}
	}

	static ui.EnumPropertyOption const m_baudRateTable[] = {
		{ "110 bps" },
		{ "300 bps" },
		{ "600 bps" },
		{ "1200 bps" },
		{ "2400 bps" },
		{ "4800 bps" },
		{ "9600 bps" },
		{ "14400 bps" },
		{ "19200 bps" },
		{ "38400 bps" },
		{ "56000 bps" },
		{ "57600 bps" },
		{ "115200 bps" },
		{ "128000 bps" },
		{ "153600 bps" },
		{ "230400 bps" },
		{ "256000 bps" },
		{ "460800 bps" },
		{ "921600 bps" },
	}

	static ui.EnumPropertyOption const m_dataBitsTable[] = {
		{ "7 bits", 7 },
		{ "8 bits", 8 },
	}

	static ui.EnumPropertyOption const m_stopBitsTable[] = {
		{ "1 bit",    io.SerialStopBits._1 },
		{ "1.5 bits", io.SerialStopBits._15 },
		{ "2 bits",   io.SerialStopBits._2 },
	}

	static ui.EnumPropertyOption const m_parityTable[] = {
		{ "None",  io.SerialParity.None },
		{ "Odd",   io.SerialParity.Odd },
		{ "Even",  io.SerialParity.Even },
		{ "Mark",  io.SerialParity.Mark },
		{ "Space", io.SerialParity.Space },
	}

	static ui.EnumPropertyOption const m_flowControlTable[] = {
		{ "None",     io.SerialFlowControl.None },
		{ "RTS/CTS",  io.SerialFlowControl.RtsCts },
		{ "XON/XOFF", io.SerialFlowControl.XonXoff },
	}

public:
	void createForm(
		ui.FormLayout* layout,
		SettingId mask = SettingId.All
	);

	void createProperties(
		ui.PropertyGrid* propertyGrid,
		ui.GroupProperty* groupProp = null,
		SettingId mask = SettingId.All
	);

	void load(doc.Storage* storage);
	void save(doc.Storage* storage);
	void updateProperties();
	void applyProperties();
	void restoreDefaultProperties();
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

void SerialSettingUi.createForm(
	ui.FormLayout* layout,
	SettingId mask
) {
	if (mask & SettingId.BaudRate) {
		m_baudRateCombo = new ui.ComboBox(m_baudRateTable, countof(m_baudRateTable));
		m_baudRateCombo.m_isEditable = true;
		layout.addRow("Baud rate:", m_baudRateCombo);
	}

	if (mask & SettingId.DataBits) {
		m_dataBitsCombo = new ui.ComboBox(m_dataBitsTable, countof(m_dataBitsTable));
		layout.addRow("Data bits:", m_dataBitsCombo);
	}

	if (mask & SettingId.Parity) {
		m_parityCombo = new ui.ComboBox(m_parityTable, countof(m_parityTable));
		layout.addRow("Parity:", m_parityCombo);
	}

	if (mask & SettingId.StopBits) {
		m_stopBitsCombo = new ui.ComboBox(m_stopBitsTable, countof(m_stopBitsTable));
		layout.addRow("Stop bits:", m_stopBitsCombo);
	}

	if (mask & SettingId.FlowControl) {
		m_flowControlCombo = new ui.ComboBox(m_flowControlTable, countof(m_flowControlTable));
		layout.addRow("Flow control:", m_flowControlCombo);
	}
}

void SerialSettingUi.createProperties(
	ui.PropertyGrid* propertyGrid,
	ui.GroupProperty* groupProp,
	SettingId mask
) {
	if (mask & SettingId.BaudRate)
		m_baudRateProp = propertyGrid.createComboProperty(
			groupProp,,
			"Baud rate",
			"Enter a baud rate...",
			"Specify a serial baud rate",
			m_baudRateTable,
			countof(m_baudRateTable)
		);

	if (mask & SettingId.DataBits)
		m_dataBitsProp = propertyGrid.createEnumProperty(
			groupProp,,
			"Data bits",
			"Serial data bit count (word size)",
			m_dataBitsTable,
			countof(m_dataBitsTable)
		);

	if (mask & SettingId.Parity)
		m_parityProp = propertyGrid.createEnumProperty(
			groupProp,,
			"Parity",
			"Serial parity check",
			m_parityTable,
			countof(m_parityTable)
		);

	if (mask & SettingId.StopBits)
		m_stopBitsProp = propertyGrid.createEnumProperty(
			groupProp,,
			"Stop bits",
			"Serial stop bit count",
			m_stopBitsTable,
			countof(m_stopBitsTable)
		);

	if (mask & SettingId.FlowControl)
		m_flowControlProp = propertyGrid.createEnumProperty(
			groupProp,,
			"Flow control",
			"Serial flow control",
			m_flowControlTable,
			countof(m_flowControlTable)
		);
}

void SerialSettingUi.load(doc.Storage* storage) {
	m_baudRate = storage.readInt("baudRateVal", Defaults.BaudRate);
	m_dataBits = storage.readInt("dataBitsVal", Defaults.DataBits); // 8 bits
	m_parity = (io.SerialParity) storage.readInt("parityVal", Defaults.Parity);
	m_stopBits = (io.SerialStopBits) storage.readInt("stopBitsVal", Defaults.StopBits);
	m_flowControl = (io.SerialFlowControl) storage.readInt("flowControlVal", Defaults.FlowControl);
}

void SerialSettingUi.save(doc.Storage* storage) {
	storage.writeInt("baudRateVal", m_baudRate);
	storage.writeInt("dataBitsVal", m_dataBits);
	storage.writeInt("parityVal", m_parity);
	storage.writeInt("stopBitsVal", m_stopBits);
	storage.writeInt("flowControlVal", m_flowControl);
}

void SerialSettingUi.updateProperties() {
	if (m_baudRateProp && m_baudRateCombo)
		m_baudRateProp.m_value = m_baudRateCombo.m_editText;

	if (m_dataBitsProp && m_dataBitsCombo)
		m_dataBitsProp.m_currentIndex = m_dataBitsCombo.m_currentIndex;

	if (m_parityProp && m_parityCombo)
		m_parityProp.m_currentIndex = m_parityCombo.m_currentIndex;

	if (m_stopBitsProp && m_stopBitsCombo)
		m_stopBitsProp.m_currentIndex = m_stopBitsCombo.m_currentIndex;

	if (m_flowControlProp && m_flowControlCombo)
		m_flowControlProp.m_currentIndex = m_flowControlCombo.m_currentIndex;
}

void SerialSettingUi.applyProperties() {
	if (m_baudRateProp && m_baudRateCombo)
		m_baudRateCombo.m_editText = m_baudRateProp.m_value;

	if (m_dataBitsProp && m_dataBitsCombo)
		m_dataBitsCombo.m_currentIndex = m_dataBitsProp.m_currentIndex;

	if (m_parityProp && m_parityCombo)
		m_parityCombo.m_currentIndex = m_parityProp.m_currentIndex;

	if (m_stopBitsProp && m_stopBitsCombo)
		m_dataBitsCombo.m_currentIndex = m_stopBitsProp.m_currentIndex;

	if (m_flowControlProp && m_flowControlCombo)
		m_flowControlCombo.m_currentIndex = m_flowControlProp.m_currentIndex;
}

void SerialSettingUi.restoreDefaultProperties() {
	if (m_baudRateProp)
		m_baudRateProp.m_value = $"$(Defaults.BaudRate) bps";

	if (m_dataBitsProp)
		m_dataBitsProp.m_value = Defaults.DataBits;

	if (m_parityProp)
		m_parityProp.m_value = Defaults.Parity;

	if (m_stopBitsProp)
		m_stopBitsProp.m_value = Defaults.StopBits;

	if (m_flowControlProp)
		m_flowControlProp.m_value = Defaults.FlowControl;
}

//..............................................................................

bool isSerialPortLess(
	io.SerialPortDesc const* port1,
	io.SerialPortDesc const* port2
) {
	return strcmp(port1.m_deviceName, port2.m_deviceName) < 0;
}

void enumerateSerialPorts(
	ui.ComboBox* portCombo,
	ui.ComboProperty* portProp
) {
	char const* editText = portCombo.m_editText; // save edit text
	portCombo.clear();

	size_t count;
	io.SerialPortDesc const* portList = io.enumerateSerialPorts(io.SerialPortDescMask.All, &count);

	std.Array portArray;
	portArray.setCount(count);
	io.SerialPortDesc const* port = portList;
	for (size_t i = 0; i < count; i++, port = port.m_next)
		portArray[i] = port;

	portArray.sort(isSerialPortLess);

	ui.EnumPropertyOption* optionArray = new ui.EnumPropertyOption[count];
	ui.EnumPropertyOption* option = optionArray;

	std.StringBuilder description;
	std.StringBuilder toolTip;

	for (size_t i = 0; i < count; i++, option++) {
		port = portArray[i];
		description = port.m_deviceName;
		toolTip = $"<table><tr><td>Device&nbsp;name:</td><td>$(port.m_deviceName)</td></tr>";

		if (port.m_description) {
			description += $" - $(port.m_description)";
			toolTip += $"<tr><td>Description:</td><td>$(port.m_description)</td></tr>";
		}

		if (port.m_manufacturer) {
			description += $" by $(port.m_manufacturer)";
			toolTip += $"<tr><td>Manufacturer:</td><td>$(port.m_manufacturer)</td></tr>";
		}

		if (port.m_hardwareIds)
			toolTip += $"<tr><td>Hardware&nbsp;ID:</td><td>$(port.m_hardwareIds)</td></tr>";

		if (port.m_driver)
			toolTip += $"<tr><td>Driver:</td><td>$(port.m_driver)</td></tr>";

		if (port.m_location)
			toolTip += $"<tr><td>Location:</td><td>$(port.m_location)</td></tr>";

		toolTip.append("</table>");

		char const* text = description.detachString();
		size_t i = portCombo.addItem(text, port.m_deviceName);
		portCombo.m_itemToolTip[i] = toolTip.detachString();

		option.m_text = text;
		option.m_value = port.m_deviceName;
	}

	portCombo.m_editText = editText; // restore edit text
	portProp.setOptions(optionArray, count);
}

//..............................................................................

//! @}

} // namespace ui
