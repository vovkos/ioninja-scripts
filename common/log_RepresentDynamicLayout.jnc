import "log_Representation.jnc"
import "log_RepresentStruct.jnc"
import "ui_ColorRangeTree.jnc"

namespace log {

//..............................................................................

enum DynamicLayoutBinPartCode {
	Raw     = 1,
	Payload = 2,
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

typedef string_t ToStringFunc(void const* p);

string_t getItemName(jnc.ModuleItemDecl* decl) {
	string_t name = decl.findAttributeValue("displayName");
	if (!name)
		name = decl.m_name;

	string_t attr = decl.findAttributeValue("displayNameAttr");
	return attr ? $"%1%2\e[m"(attr, name) : name;
}

string_t getItemDigest(
	jnc.ModuleItemDecl* decl,
	void const* p
) {
	ToStringFunc thin* digestFunc = decl.findAttributeValue("digestFunc");
	return digestFunc ? digestFunc(p) : null;
}

string_t getItemValue(
	jnc.ModuleItemDecl* decl,
	jnc.Type* type,
	void const* p,
	jnc.PtrTypeFlags ptrTypeFlags = 0,
	uint_t bitOffset = 0,
	uint_t bitCount = 0
) {
	string_t valueString;

	ToStringFunc thin* formatFunc = decl.findAttributeValue("formatFunc");
	if (formatFunc)
		return formatFunc(p);

	string_t formatSpec = decl.findAttributeValue("formatSpec");
	jnc.Type* displayType = decl.findAttributeValue("displayType");

	if (!displayType)
		displayType = type;

	if (!(ptrTypeFlags & (jnc.PtrTypeFlags.BitField | jnc.PtrTypeFlags.BigEndian)))
		return displayType.getValueString(p, formatSpec);

	jnc.DataPtrType* displayPtrType = (ptrTypeFlags & jnc.PtrTypeFlags.BitField) ?
		displayType.getDataPtrType(bitOffset, bitCount,,, ptrTypeFlags) :
		displayType.getDataPtrType(,, ptrTypeFlags);

	return displayPtrType.getTargetValueString(p, formatSpec);
}

void setItemBackColor(
	Representation* representation,
	ui.ColorRangeTree* colorRangeTree,
	jnc.ModuleItemDecl* decl,
	size_t offset,
	size_t length
) {
	jnc.Attribute* attr = decl.findAttribute("backColor");
	if (!attr)
		return;

	uint_t color = attr.m_value;
	uint_t prevColor = representation.m_lineAttr.m_backColor;
	representation.m_lineAttr.m_backColor = color;
	colorRangeTree.add(offset, length, color);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

void representDynamicLayout(
	Representation* representation,
	jnc.DynamicLayout* layout,
	uint_t foldFlags
) {
	ui.ColorRangeTree colorRangeTree;

	if (!representDynamicSectionGroup(
		representation,
		colorRangeTree,
		layout,
		DynamicLayoutBinPartCode.Raw,
		layout.m_p,
		0,
		foldFlags
	))
		return;

	representation.addBreak();
	representation.m_lineAttr.m_textColor = ui.StdColor.BrightBlack;
	representation.m_lineAttr.m_backColor = ui.ColorFlags.Undefined;
	representation.addBin(DynamicLayoutBinPartCode.Raw, layout.m_p, layout.m_size);

	size_t rangeCount = colorRangeTree.m_rangeCount;
	if (rangeCount <= 1) // only retro-colorize if there are multiple colors!
		return;

	ui.ColorRange* ranges = new ui.ColorRange[rangeCount];
	colorRangeTree.flatten(ranges, rangeCount);

	size_t end = layout.m_size;
	for (size_t i = 0; i < rangeCount; i++) {
		ui.ColorRange range = ranges[i];
		ui.TextAttr attr = { ui.ColorFlags.Undefined, range.m_color, ui.FontFlags.Undefined	}
		representation.retroColorize(range.m_offset - layout.m_size, range.m_length, attr);
	}
}

// returns true if expanded

bool representDynamicSectionGroup(
	Representation* representation,
	ui.ColorRangeTree* colorRangeTree,
	jnc.DynamicSectionGroup* group,
	uint64_t binPartCode = 0,
	void const* p0,
	size_t offset0 = 0,
	uint_t foldFlags = -1,
	string_t indent = ""
) {
	uint_t baseBackColor = representation.m_lineAttr.m_backColor;

	for (size_t i = 0; i < group.m_sectionCount; i++) {
		jnc.DynamicSection* section = group.m_sectionArray[i];
		void const* p = p0 + section.m_offset;
		size_t offset = offset0 + section.m_offset;

		switch (section.m_sectionKind) {
		case jnc.DynamicSectionKind.Struct:
			representFields(
				representation,
				colorRangeTree,
				dynamic (jnc.StructType*)section.m_type,
				binPartCode,
				p,
				offset,
				foldFlags,
				indent
			);
			break;

		case jnc.DynamicSectionKind.Array:
			setItemBackColor(
				representation,
				colorRangeTree,
				section.m_decl,
				offset,
				section.m_size
			);

			representArray(
				representation,
				colorRangeTree,
				section.m_decl,
				section.m_type,
				section.m_elementCount,
				binPartCode,
				p,
				offset,
				foldFlags,
				indent
			);

			break;

		case jnc.DynamicSectionKind.Group:
			string_t subIndent;

			setItemBackColor(
				representation,
				colorRangeTree,
				section.m_decl,
				offset,
				section.m_size
			);

			if (section.m_decl.findAttribute("ungroup"))
				subIndent = indent;
			else {
				if (!representGroupHeader(representation, section.m_decl, p, foldFlags, indent))
					break;

				subIndent = indent + "    ";
			}

			representDynamicSectionGroup(
				representation,
				colorRangeTree,
				section,
				binPartCode,
				p0,      // section group offsets are relative to the layout, not to parent group
				offset0, // so we have to use layout offset and base
				foldFlags,
				subIndent
			);
			break;
		}

		representation.m_lineAttr.m_backColor = baseBackColor; // restore
	}

	return true;
}

// returns true if expanded

bool representGroupHeader(
	Representation* representation,
	jnc.ModuleItemDecl* decl,
	void const* p,
	uint_t foldFlags,
	string_t indent
) {
	string_t name = getItemName(decl);
	uint_t foldFlag = decl.findAttributeValue("foldFlag");

	// always add \t to preserve the name-value table column position

	if (!foldFlag)
		representation.addHyperText($"%1%2\t"(indent, name));
	else if (foldFlags & foldFlag)
		representation.addHyperText($"%1[\e^-%2\e[34m-\e[m] %3\t"(
			indent,
			foldFlag,
			name
		));
	else {
		string_t digest = getItemDigest(decl, p);
		if (digest.m_length)
			representation.addHyperText($"%1[\e^+%2\e[34m+\e[m] %3:\t%4"(
				indent,
				foldFlag,
				name,
				digest
			));
		else
			representation.addHyperText($"%1[\e^+%2\e[34m+\e[m] %3\t"(
				indent,
				foldFlag,
				name
			));

		return false;
	}

	return true;
}

void representArray(
	Representation* representation,
	ui.ColorRangeTree* colorRangeTree,
	jnc.ModuleItemDecl* decl,
	jnc.Type* elementType,
	size_t elementCount,
	uint64_t binPartCode,
	void const* p,
	size_t offset,
	uint_t foldFlags,
	string_t indent
) {
	if (!representGroupHeader(representation, decl, p, foldFlags, indent))
		return;

	size_t elementSize = elementType.m_size;

	if (decl.findAttribute("binary")) {
		representation.addBin(0, p, elementSize * elementCount);
		return;
	}

	indent += "    ";

	if (elementType.m_typeKindFlags & jnc.TypeKindFlags.Derivable) {
		jnc.DerivableType* derivableType = dynamic (jnc.DerivableType*)elementType;
		string_t fieldIndent = indent + "    ";
		for (size_t i = 0; i < elementCount; i++) {
			representation.addHyperText($"%1[%2]"(indent, i));
			representFields(
				representation,
				colorRangeTree,
				derivableType,
				binPartCode,
				p,
				offset,
				-1, // no folds for array elements
				fieldIndent
			);

			p += elementSize;
			offset += elementSize;
		}
	} else
		for (size_t i = 0; i < elementCount; i++) {
			string_t value = getItemValue(decl, elementType, p);
			representation.addHyperText($"%1[%2]:\t\e^!0x%x,%4,%5\e[34m%6"(
				indent,
				i,
				binPartCode,
				offset,
				elementSize,
				value
			));

			p += elementSize;
			offset += elementSize;
		}
}

void representFields(
	log.Representation* representation,
	ui.ColorRangeTree* colorRangeTree,
	jnc.DerivableType* type,
	uint64_t binPartCode,
	void const* p0,
	size_t offset0,
	uint_t foldFlags,
	string_t indent
) {
	uint_t baseBackColor = representation.m_lineAttr.m_backColor;

	string_t fieldIndent = indent + "    ";
	size_t count = type.m_fieldCount;
	for (size_t i = 0; i < count; i++) {
		jnc.Field* field = type.m_fieldArray[i];
		jnc.Type* fieldType = field.m_type;
		void const* p = p0 + field.m_offset;
		size_t offset = offset0 + field.m_offset;

		setItemBackColor(
			representation,
			colorRangeTree,
			field,
			offset,
			fieldType.m_size
		);

		if (fieldType.m_typeKindFlags & jnc.TypeKindFlags.Derivable) {
			jnc.DerivableType* derivableType = dynamic (jnc.DerivableType*)fieldType;
			if (field.findAttribute("ungroup"))
				representFields(
					representation,
					colorRangeTree,
					derivableType,
					binPartCode,
					p,
					offset,
					foldFlags,
					indent
				);
			else if (representGroupHeader(representation, field, p, foldFlags, indent))
				representFields(
					representation,
					colorRangeTree,
					derivableType,
					binPartCode,
					p,
					offset,
					foldFlags,
					fieldIndent
				);
		} else {
			string_t name = getItemName(field);
			string_t value = getItemValue(
				field,
				fieldType,
				p,
				field.m_ptrTypeFlags,
				field.m_bitOffset,
				field.m_bitCount
			);

			representation.addHyperText($"%1%2:\t\e^!0x%x,%4,%5\e[34m%6"(
				indent,
				name,
				binPartCode,
				offset,
				fieldType.m_size,
				value
			));
		}

		representation.m_lineAttr.m_backColor = baseBackColor; // restore
	}
}

//..............................................................................

} // namespace log
